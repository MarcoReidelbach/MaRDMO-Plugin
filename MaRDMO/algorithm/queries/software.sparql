SELECT DISTINCT
?DOI ?SWMATH ?SOURCECODE_URL ?DESCRIPTION_URL
(GROUP_CONCAT(DISTINCT CONCAT(?bench, " || ", ?benchl, " || ", ?benchd); separator=" <|> ") AS ?tested_by)
(GROUP_CONCAT(DISTINCT CONCAT(?pub, " || ", ?publ, " || ", ?pubd); separator=" <|> ") AS ?publication)

WHERE {{
    VALUES ?id {{ wd:{0} }}

    OPTIONAL {{ 
      ?id wdt:{swMath work ID} ?SWMATH
    }}

    OPTIONAL {{ 
      ?id wdt:{DOI} ?DOI
    }}

    OPTIONAL {{
      ?id wdt:{source code repository URL} ?SOURCECODE_URL.
    }}

    OPTIONAL {{
      ?id wdt:{described at URL} ?DESCRIPTION_URL.
    }}

    OPTIONAL {{
        ?benchraw wdt:{tested by} ?id. 
        BIND(CONCAT("mardi:", STRAFTER(STR(?benchraw), STR(wd:))) AS ?bench)

        ?benchraw wdt:{instance of} wd:{benchmark}.

        OPTIONAL {{
            ?benchraw rdfs:label ?benchlraw.
            FILTER (lang(?benchlraw) = 'en')
        }}
        BIND(COALESCE(?benchlraw, "No Label Provided!") AS ?benchl)

        OPTIONAL {{
            ?benchraw schema:description ?benchdraw
            FILTER (lang(?benchdraw) = 'en')
        }}
        BIND(COALESCE(?benchdraw, "No Description Provided!") AS ?benchd)
    }}
  
    OPTIONAL {{
        ?id p:{described by source} ?statement_p .
        ?statement_p ps:{described by source} ?pubraw .
        ?statement_p pq:{object has role} ?role .
        VALUES ?role {{ wd:{documentation} wd:{use} }}
        BIND(CONCAT("mardi:", STRAFTER(STR(?pubraw), STR(wd:))) AS ?pub)

        OPTIONAL {{
            ?pubraw rdfs:label ?publraw .
            FILTER (lang(?publraw) = 'en')
        }}
        BIND(COALESCE(?publraw, "No Label Provided!") AS ?publ)

        OPTIONAL {{
            ?pubraw schema:description ?pubdraw .
            FILTER (lang(?pubdraw) = 'en')
        }}
        BIND(COALESCE(?pubdraw, "No Description Provided!") AS ?pubd)
    }}
}}
GROUP BY ?DOI ?SWMATH ?SOURCECODE_URL ?DESCRIPTION_URL

